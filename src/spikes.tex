\chapter{spikes}
\label{sec:attract_mode}
\lhead[tempest]{}
\lstset{style=6502Style}

Green spikes grow from the bottom of the well. They advance and retreat in a regular fashion until they
are struck by a player bullet. When this happen a spike with a white dot is formed, presenting a peril
to the player when they descend the well at the end of the level. Each
time we draw a spike, the below is an approximation of the effect we want to achieve - a green line
advancing from the far point of the well with a white dot at the tip.

\begin{minipage}[c]{0.48\linewidth}
\vspace{-0.6cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=8cm,center}
      \includegraphics{src/spikes/spike_evolution_blank.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}
\begin{minipage}[c]{0.06\linewidth}
\begin{figure}[H]
    \centering
    \begin{tikzpicture}[baseline={(current bounding box.south)}]
      \draw[->,line width=3pt] (0,0) to (1,0);
    \end{tikzpicture}
\end{figure}
\end{minipage}
\begin{minipage}[c]{0.48\linewidth}
\vspace{-0.6cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=8cm,center}
      \includegraphics{src/spikes/spike_shooting_2.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}

We draw the spikes, like we draw everything else in \textit{Tempest}, by adding appropriate vector generator opcodes to the display
list (the \icode{VGLIST}). We covered the basic details of this in 
\hyperref[sec:vector]{\textcolor{blue}{'tracing the beam.'}} 

When we look at the routine responsible for drawing spikes, \icode{DSPENL}, we find that we start off with a very simple opcode:

\begin{lstlisting}
        .SBTTL  DISPLAY - ENEMY LINES
DSPENL: LDA I,1        ; STORE 1 IN A.
        JSR VGSCA1     ; SET NORMAL SCALE.
        LDA VGLIST     ; GET 1ST BYTE OF SCALE OPCODE.
        PHA            ; STORE IT TO STACK.
        LDA VGLIST+1   ; GET 2ND BYTE OF SCALE OPCODE.
        PHA            ; STORE IT TO STACK.
\end{lstlisting}

This adds a pair of bytes to the display list, which together form the opcode for
setting the scale to 'full size'.
\begin{figure}[H]
  {
    \setlength{\tabcolsep}{3.0pt}
    \setlength\cmidrulewidth{\heavyrulewidth} % Make cmidrule = 
    \begin{adjustbox}{width=12.5cm,center}
      \begin{tabular}{lll}
        \toprule
        Vector Description & Op Code & Bitmap \\
        \midrule
        Set Scale~~~~~~~~~~~~~~~~~~~~ & \icode{0x7100~~~~~~~} & \icode{10000001 00000000~~~~~~~~~~~~~~~~~~} \\
      \end{tabular}
    \end{adjustbox}
  }
\end{figure}
\vspace{-0.6cm}

Our next step is to iterate through all of the active spikes and draw each one.
The number of active spikes is stored in \icode{NLINES}. We are going to use this number as a counter
and draw a spike until we decrement our counter to 0. We will also use this number as an index into arrays
that provide information about the state of each spike.

\begin{lstlisting}
        LDA I,0            ; STORE 0 IN A.
        STA INDEX2         ; AND SET AS LINE LOOP INDEX.
        STA VGY            ; AND SET AS INITAL INDEX IN VGLIST.
        LDX I,NLINES-1     ; GET NUMBER OF SPIKES.
        STX INDEX1         ; AND SET AS OUR INDEX.
        BEGIN              ; LOOP FOR EACH LINE FROM 0 TO TOP
          LDX I,3          ; SET X TO 3.
          LDY VGY          ; STORE CUR POS IN VGLIST IN Y.
          BEGIN            ; LOOP THROUGH ENLFIX AND STORE IN VGLIST
            LDA X,ENLFIX   ; GET CURRENT BYTE FROM ENLFIX.
            STA NY,VGLIST  ; STORE IT IN DISPLAY LIST.
            INY            ; INCREMENT VGLIST POSITION.
            DEX            ; MOVE TO NEXT BYTE IN ENLFIX.
          MIEND            ; LOOP UNTIL END.
          STY VGY          ; STORE CUR POS IN VGLIST IN VGY.
\end{lstlisting}
As you may notice above, for
each line we start by adding some 'static' opcodes that are common to all spikes. These are found in the
\icode{ENLFIX} array.
\begin{lstlisting}
ENLFIX: .BYTE 80,40,68,05
\end{lstlisting}

The contents of \icode{ENLFIX} provides two new opcodes to the \icode{VGLIST}, centering the drawing position and setting the color
of our 'pen' to green:

\begin{figure}[H]
  {
    \setlength{\tabcolsep}{3.0pt}
    \setlength\cmidrulewidth{\heavyrulewidth} % Make cmidrule = 
    \begin{adjustbox}{width=12.5cm,center}
      \begin{tabular}{lll}
        \toprule
        Vector Description & Op Code & Bitmap \\
        \midrule
        Set Scale~~~~~~~~~~~~~~~~~~~~ & \icode{0x7100~~~~~~~} & \icode{10000001 00000000~~~~~~~~~~~~~~~~~~} \\
        Set Color to Green~~~~~~~~~~~ & \icode{0x6805}        & \icode{01101000 00001001} \\
        Center~~~~~~~~~~~~~~~~~~~~~~~ & \icode{0x8040~~~~~~~} & \icode{10000000 01000000} \\
      \end{tabular}
    \end{adjustbox}
  }
\end{figure}
\vspace{-0.6cm}

Our next step is to draw the actual line. To do this we just need to retrieve and update the
'far point' for each line. For each line we keep an X and Y co-ordinate in two separate sets of arrays.
Since the co-ordinate values can be greater than 255 we will need two bytes to express each. For this
reason we have two arrays for the X co-ordinate, one that stores the 'low byte' for each line (\icode{LIFSXL}), and
another that stores the 'high byte' (\icode{LIFSXH}). We do the same for the Y co-ordinate (\icode{LIFSZL} and \icode{LIFSZH}).
In order to calculate the point to which we should draw the line we get whatever the current value is and increment with whatever
the value is in the line adjacent to it.

\begin{lstlisting}
FIXSTU: ;CALCULATE SCREEN LOCATION OF FAR POINT
        LDA INDEX2    ; GET THE CURRENT LINE NUMBER.
        TAX           ; STORE IT IN X.
        CLC           ; CLEAR THE CARRY BIT.
        ADC I,1       ; ADD 1 TO GET INDEX TO ADJACENT LINE.
        AND I,0F      ; CLAMP IT TO BETWEN 0 AND 15.
        TAY           ; STORE THE ADJACENT LINE INDEX IN Y.

        LDA X,LIFSXL  ; GET THE LOW X-COORDINATE BYTE FOR CUR LINE.
        SEC           ; ADD ANY CARRIED VALUE.
        ADC Y,LIFSXL  ; ADD VALUE FROM ADJACENT LINE.
        STA SXL       ; STORE AS NEW X POS LOW BYTE.

        LDA X,LIFSXH  ; GET THE HIGH X-COORDINATE BYTE FOR CUR LINE.
        ADC Y,LIFSXH  ; ADD VALUE FROM ADJACENT LINE.
        STA SXH       ; STORE AS NEW X POS HIGH BYTE.

        ASL
        ROR SXH       ; DIVIDE BY 2 TO GET MIDWAY POINT.
        ROR SXL       ; DIVIDE BY 2 TO GET MIDWAY POINT.

        LDA X,LIFSZL  ; GET THE LOW Y-COORDINATE BYTE FOR CUR LINE.
        SEC           ; ADD ANY CARRIED VALUE.
        ADC Y,LIFSZL  ; ADD VALUE FROM ADJACENT LINE.
        STA SZL       ; STORE AS NEW Y POS LOW BYTE.

        LDA X,LIFSZH  ; GET THE HIGH Y-COORDINATE BYTE FOR CUR LINE.
        ADC Y,LIFSZH  ; ADD VALUE FROM ADJACENT LINE.
        STA SZH       ; STORE AS NEW Y POS HIGH BYTE.

        ASL
        ROR SZH       ; DIVIDE BY 2 TO GET MIDWAY POINT.
        ROR SZL       ; DIVIDE BY 2 TO GET MIDWAY POINT.
\end{lstlisting}

Now we add the values we have calculated above to the \textit{Display List} (\icode{VGLIST}) as a 4 byte opcode that encodes
a \textit{Draw Relative Vector} instruction. The special thing about this line is that it is invisible. It is invisible
because what we are actually doing is moving the 'pen' from the center of the screen to the 'far point' at which the green
spike line will begin. The instruction for this blank line consists of the X and Y co-ordinates
to which we should draw the invisible line from its origin at the center of the screen.

As we mentioned above there are four bytes in total. Two bytes
for the X co-ordinate and two for the Y co-ordinate. In each case the high byte has a special property that makes the line
we draw work the way we want it to. For the X co-ordinate the top three bits are zero so that the opcode is correctly identified
as a \textit{Draw Relative Vector} instruction. For the Y co-ordinate the top three bits are zero so that the intensity (i.e. the color)
of the line is invisible. Any value at all in these three bits (between 0 and 7) will make the line progressively more visible to the
player.

\begin{lstlisting}
YVGVCT: LDY VGY       ; PUT CUR POS IN VGLIST IN Y
        LDA SZL       ; PUT Y-COORDINATE LOW BYTE IN A.
        STA NY,VGLIST ; STORE Y-COORDINATE LOW BYTE IN VGLIST.
        INY           ; INCREMENT CUR POS IN VG LIST.
        STA CURNTY    ; STASH LOW BYTE IN CURNTY.
        LDA SZH       ; PUT Y-COORDINATE HIGH BYTE IN A.
        STA CURNTY+1  ; STASH HIGH BYTE IN CURNTY.
        ; MAKE SURE TOP 3 BITS ARE ZERO SO THAT THIS IS A
        ; 'DRAW RELATIVE VECTOR' INSTRUCTION.
        AND I,1F      ; CLAMP HIGH BYTE TO LAST 5 BITS.
        STA NY,VGLIST ; STORE Y-COORDINATE HIGH BYTE IN VGLIST.
        INY           ; INCREMENT CUR POS IN VG LIST.
        LDA SXL       ; PUT X-COORDINATE LOW BYTE IN A.
        STA NY,VGLIST ; STORE X-COORDINATE LOW BYTE IN VGLIST.
        INY           ; INCREMENT CUR POS IN VG LIST.
        STA CURNTX    ; STASH LOW BYTE IN CURNTX.
        LDA SXH       ; PUT X-COORDINATE HIGH BYTE IN A.
        STA CURNTX+1  ; STASH HIGH BYTE IN CURNTX.
        ; MAKE SURE TOP 3 BITS ARE ZERO SO THAT THE LINE
        ; HAS 'ZERO INTENSITY', I.E. NO COLOR.
        AND I,1F      ; CLAMP HIGH BYTE TO LAST 5 BITS.
        STA NY,VGLIST ; STORE X-COORDINATE HIGH BYTE IN VGLIST.
        INY           ; INCREMENT CUR POS IN VG LIST.
        STY VGY       ; STORE CUR POS IN VG LIST.
        RTS           ; WE'RE DONE - RETURN.
\end{lstlisting}

With this, our current list of opcodes in \icode{VGLIST} now looks like this.
\begin{figure}[H]
  {
    \setlength{\tabcolsep}{3.0pt}
    \setlength\cmidrulewidth{\heavyrulewidth} % Make cmidrule = 
    \begin{adjustbox}{width=12.5cm,center}
      \begin{tabular}{lll}
        \toprule
        Vector Description & Op Code & Bitmap \\
        \midrule
        Set Scale                     & \icode{0x7100}        & \icode{01110001 00000000} \\
        Set Color to Green            & \icode{0x6805}        & \icode{01101000 00001001} \\
        Center                        & \icode{0x8040}        & \icode{10000000 01000000} \\
        Draw Relative Vector (Blank)  & \icode{0x0021 0x0034} & \icode{00000000 00010001 00000000 00111000} \\
      \end{tabular}
    \end{adjustbox}
  }
\end{figure}
\vspace{-0.6cm}

Just to reiterate: we still haven't drawn any part of our line yet. We have simply moved our pen to the origin of
the spike line we are about to draw, indicated by the green dot below.
\vspace{-1cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=9cm,center}
      \includegraphics{src/spikes/spike_evolution_spike_origin.png}%
    \end{adjustbox}
\end{figure}
\vspace{-0.4cm}

So our next step is to draw a line from this origin. The routine \icode{TIPACT}, which is responsible
for drawing both the line and the item at its tip, starts out by figuring out the midway point between
the web lines. With this it will be able to project a line from the furthest point of the screen to the
nearest.

\begin{lstlisting}
        .SBTTL  DISPLAY - ENEMY LINES (TIP STUFF)
TIPACT: LDX INDEX2    ; PUT INDEX TO CURRENT LINE IN X.
        LDA X,LINEY   ; GET Y POS OF CURRENT LINE.
        STA PYL       ; STORE IN PYL.
        ; CALCULATE NEAR PT.
        LDA X,LINEXM  ; GET X COORD OF MIDWAY PT BETWEEN LINES.
        STA PXL       ; STORE IN PXL.
        LDA X,LINEZM  ; GET Y COORD OF MIDWAY PT BETWEEN LINES..
        STA PZL       ; STORE IN PZL.
        JSR WORSCR    ; CALCULATE SXL, SYL, SZL: NEAR POINT OF LINE.
\end{lstlisting}

This is an involved business
performed by the \icode{WORSCR} routine in the last line above. It is so long and complicated that I have not attempted to annotate
it here. For our purposes, we just need to know that it uses the values in \icode{PXL}, \icode{PYL}, and \icode{PZL}
to perform this calculation (this is origin) and the end result is captured in the \icode{SXL}/\icode{SXH} and
\icode{SZL}/\icode{SZH} high/low byte pairs (this is the endpoint of the line).

\begin{minipage}[c]{0.31\linewidth}
\begin{lstlisting}
WORSCR: LDA PYL
        SEC
        SBC EYL
        STA MXPL
        LDA I,0
        SBC EYH
        STA MXPH
        IFMI       
        LDA I,0   
        STA MXPH
        LDA I,1
        STA MXPL
        ENDIF
        LDA PZL
        CMP EZL
        IFCS
        SBC EZL
        LDX I,0
        ELSE
        LDA EZL
        SEC
        SBC PZL
        LDX I,-1
        ENDIF
        STA MZLH
        STA MSZXD
        STX MTEMP+2
        LDA PXL
        CMP EXL
        IFCS
        SBC EXL
        LDX I,0
        ELSE
        LDA EXL
        SEC
        SBC PXL
        LDX I,-1
        ENDIF
\end{lstlisting}
\end{minipage}
\hspace{0.5cm}
\begin{minipage}[c]{0.31\linewidth}
\begin{lstlisting}
    STA MTEMP+1
    STX MTEMP+3
    BEGIN
    BIT MSTAT
    PLEND
    LDA MYLOW
    STA SZL
    LDA MYHIGH
    STA SZH
    LDA MTEMP+1
    STA MZLH
    STA MSZXD
    LDA MTEMP+2
    IFPL
    LDA SZL
    CLC
    ADC ZADJL
    STA SZL
    LDA SZL+1
    ADC ZADJL+1
    IFVS
    LDA I,0FF
    STA SZL
    LDA I,7F
    ENDIF
    STA SZL+1
    ELSE
    LDA ZADJL
    SEC
    SBC SZL
    STA SZL
    LDA ZADJL+1
    SBC SZL+1
    IFVS
    LDA I,0
    STA SZL
    LDA I,80
    ENDIF
\end{lstlisting}
\end{minipage}
\hspace{0.5cm}
\begin{minipage}[c]{0.31\linewidth}
\begin{lstlisting}
    STA SZL+1
    ENDIF
    BEGIN
    BIT MSTAT
    PLEND
    LDA MYLOW
    STA SXL
    LDA MYHIGH
    STA SXH
    LDX MTEMP+3
    IFPL
    LDA SXL
    CLC
    ADC XADJL
    STA SXL
    LDA SXL+1
    ADC XADJL+1
    IFVS
    LDA I,0FF
    STA SXL
    LDA I,7F
    ENDIF
    STA SXL+1
    RTS
    ENDIF
    LDA XADJL
    SEC
    SBC SXL
    STA SXL
    LDA XADJL+1
    SBC SXL+1
    IFVS
    LDA I,0
    STA SXL
    LDA I,80
    ENDIF
    STA SXL+1
    RTS
\end{lstlisting}
\end{minipage}

With these in hand, i.e. the co-ordinates of our near point, we can now draw our line vector. 
As before, this means calculating the values for our \textit{Draw Relative Vector} instruction.
\clearpage

\begin{lstlisting}
FCONNEC:LDY VGY        ; STORE THE CURRENT POS IN VGLIST IN Y.
        LDA SZL        ; GET THE LOW BYTE OF THE CALCULATED Y POSITION.
        SEC            ; SET CARRY.
        SBC CURNTY     ; SUBTRACT THE LOW BYTE OF THE ORIGIN.
        STA NY,VGLIST  ; STORE IN THE VGLIST.
        INY            ; INCREMENT POSITION IN THE VGLIST.
        LDA SZH        ; GET THE HIGH BYTE OF THE CALCULATED Y POSITION.
        SBC CURNTY+1   ; SUBTRACT THE HIGH BYTE OF THE ORIGIN.
        AND I,1F       ; CLAMP TO THE LOWER 5 BITS (SO INTENSITY NOT SET).
        STA NY,VGLIST  ; STORE IN THE VGLIST.
        INY            ; INCREMENT POSITION IN THE VGLIST.
        LDA SXL        ; GET THE LOW BYTE OF THE CALCULATED X POSITION.
        SEC            ; SET CARRY.
        SBC CURNTX     ; SUBTRACT THE LOW BYTE OF THE ORIGIN.
        STA NY,VGLIST  ; STORE IN THE VGLIST.
        INY            ; INCREMENT POSITION IN THE VGLIST.
        LDA SXH        ; GET THE HIGH BYTE OF THE CALCULATED Y POSITION.
        SBC CURNTX+1   ; SUBTRACT THE HIGH BYTE OF THE ORIGIN.
        AND I,1F       ; CLAMP TO THE LOWER 5 BITS (SO INTENSITY NOT SET).
        ORA I,0A0      ; MAKE SURE THE LINE IS VISIBLE BY SETTING INTENSITY TO A0..
        STA NY,VGLIST  ; STORE IN THE VGLIST.
        INY            ; INCREMENT THE POSITION IN THE VGLIST.
        STY VGY        ; STORE THE NEW CURRENT POSITION IN THE VGLIST.
        RTS
\end{lstlisting}

And now our updated list of opcode instructions looks as follows. Note that we make the line visible by setting
the topmost three bits in the second-last byte.
\begin{figure}[H]
  {
    \setlength{\tabcolsep}{3.0pt}
    \setlength\cmidrulewidth{\heavyrulewidth} % Make cmidrule = 
    \begin{adjustbox}{width=12.5cm,center}
      \begin{tabular}{lll}
        \toprule
        Vector Description & Op Code & Bitmap \\
        \midrule
        Set Scale                     & \icode{0x7100}        & \icode{01110001 00000000} \\
        Set Color to Green            & \icode{0x6805}        & \icode{01101000 00001001} \\
        Center                        & \icode{0x8040}        & \icode{10000000 01000000} \\
        Draw Relative Vector (Blank)  & \icode{0x0021 0x0034} & \icode{00000000 00010001 00000000 00111000} \\
        Draw Relative Vector (Green)  & \icode{0x0005 0xA007}    & \icode{00000000 00001001 10100000 00000111} \\
      \end{tabular}
    \end{adjustbox}
  }
\end{figure}
\vspace{-0.6cm}

And this is what the instructions will generate on the screen:

\begin{minipage}[c]{0.48\linewidth}
\vspace{-0.6cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=8cm,center}
      \includegraphics{src/spikes/spike_evolution_spike_origin.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}
\begin{minipage}[c]{0.06\linewidth}
\begin{figure}[H]
    \centering
    \begin{tikzpicture}[baseline={(current bounding box.south)}]
      \draw[->,line width=3pt] (0,0) to (1,0);
    \end{tikzpicture}
\end{figure}
\end{minipage}
\begin{minipage}[c]{0.48\linewidth}
\vspace{-0.6cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=8cm,center}
      \includegraphics{src/spikes/spike_shooting_3.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}

Our next step is to add a 'white dot' to end of the line. 

\begin{lstlisting}
WHITIP: LDY VGY        ; PUT CUR POS IN VGLIST IN Y
        LDA I,WHITE    ; STORE 'WHITE VALUE' IN A.
        STA NY,VGLIST  ; STORE WHITE COLOR VALUE IN VGLIST.
        INY            ; INCREMENT CUR POS IN VGLIST.
        LDA I,68       ; STORE 'SET COLOR' OPCODE IN A.
        STA NY,VGLIST  ; STORE 'SET COLOR' OPCODE IN VGLIST.
        INY            ; INCREMENT CUR POS IN VGLIST.
        LDA JSRDOT     ; GET LOW BYTE OF 'DOT' ROUTINE. 
        STA NY,VGLIST  ; STORE IN VGLIST.
        INY            ; INCREMENT CUR POS IN VGLIST.
        LDA JSRDT1     ; GET HIGH BYTE OF 'DOT' ROUTINE. 
        STA NY,VGLIST  ; STORE IN VGLIST.
        INY            ; INCREMENT CUR POS IN VGLIST.
        STY VGY        ; UPDATE CUR POS IN VGLIST.
\end{lstlisting}

This adds two new opcodes to the \icode{VGLIST}. One to set the pen color to white, and another
that jumps to a subroutine that will draw our white dot. This 'white dot' routine is as simple as the following:
\begin{lstlisting}
JADOT:	VCTR 0,0,CB
        RTSL
\end{lstlisting}

Our final list of opcodes in the \icode{VGLIST} reads as follows:

\begin{figure}[H]
  {
    \setlength{\tabcolsep}{3.0pt}
    \setlength\cmidrulewidth{\heavyrulewidth} % Make cmidrule = 
    \begin{adjustbox}{width=12.5cm,center}
      \begin{tabular}{lll}
        \toprule
        Vector Description & Op Code & Bitmap \\
        \midrule
        Set Scale                     & \icode{0x7100}        & \icode{01110001 00000000} \\
        Set Color to Green            & \icode{0x6805}        & \icode{01101000 00001001} \\
        Center                        & \icode{0x8040}        & \icode{10000000 01000000} \\
        Draw Relative Vector (Blank)  & \icode{0x0021 0x0034} & \icode{00000000 00010001 00000000 00111000} \\
        Draw Relative Vector (Green)  & \icode{0x0005 0xA007}    & \icode{00000000 00001001 10100000 00000111} \\
        Set Color to White            & \icode{0x6800}    & \icode{01101000 00000000} \\
        Jump to Subroutine (JSRL JADOT)   & \icode{0xAD7F}    & \icode{10101101 01111111} \\
      \end{tabular}
    \end{adjustbox}
  }
\end{figure}
\vspace{-0.6cm}

And this is how it looks on screen:

\begin{minipage}[c]{0.48\linewidth}
\vspace{-0.9cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=8cm,center}
      \includegraphics{src/spikes/spike_shooting_3.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}
\begin{minipage}[c]{0.06\linewidth}
\begin{figure}[H]
    \centering
    \begin{tikzpicture}[baseline={(current bounding box.south)}]
      \draw[->,line width=3pt] (0,0) to (1,0);
    \end{tikzpicture}
\end{figure}
\end{minipage}
\begin{minipage}[c]{0.48\linewidth}
\vspace{-0.9cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=8cm,center}
      \includegraphics{src/spikes/spike_shooting_2.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}


\iffalse

\begin{lstlisting}
;
;LINES (SCREEN COORDINATES)
;
LINSXH:	.BLKB NLINES		;NEAR PT.
LINSXL:	.BLKB NLINES
LINSZH:	.BLKB NLINES
LINSZL:	.BLKB NLINES
LIFSXH:	.BLKB NLINES		;FAR PT.
LIFSXL:	.BLKB NLINES
LIFSZH:	.BLKB NLINES
LIFSZL:	.BLKB NLINES
LINSTA:	.BLKB NLINES		;ENEMY LINE STATUS (D7=1:NEW NEAR PT.;D6=1=SHATTERED)
\end{lstlisting}
\fi
