\chapter{game over}
\lhead[tempest 2000]{}
\label{sec:bullet}
\lstset{style=68KStyle}
The player explosion in \textit{Tempest 2000} is vector-based: that is, it is
not a solid polygon like the claw or its enemies, but a line-based object using
the same machinery as the objects in \textit{Tempest Classic} mode. The data
structure that defines the \icode{zap} used to render the explosion is a series
of x,y,z co-ordinate triplets followed by an index to the co-ordinate it should be connected to.

\begin{minipage}[c]{0.48\linewidth}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=7cm,center}
      \includegraphics[width=12cm]{src/zap/zap.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}
\hspace{0.5cm}
\begin{minipage}[c]{0.48\linewidth}
\begin{lstlisting}[basicstyle=\scriptsize\ttfamily]
\end{lstlisting}
\vspace*{\fill}
\end{minipage}
\clearpage

The explosion effect will consist of four of these star-shaped \icode{zap}s spinning
clockwise in unison and racing towards the player. We can visualize the general
character of the explosion as follows:

\begin{minipage}[c]{0.48\linewidth}
\vspace{-0.6cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=6.5cm,center}
      \includegraphics{src/zap/web_zap1.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}
\hspace{0.2cm}
\begin{minipage}[c]{0.48\linewidth}
\vspace{-0.6cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=6.5cm,center}
      \includegraphics{src/zap/web_zap2.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}
\begin{minipage}[c]{0.48\linewidth}
\vspace{-1.1cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=6.5cm,center}
      \includegraphics{src/zap/web_zap3.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}
\hspace{0.2cm}
\begin{minipage}[c]{0.48\linewidth}
\vspace{-1.1cm}
\begin{figure}[H]
    \centering
    \begin{adjustbox}{width=6.5cm,center}
      \includegraphics{src/zap/web_zap4.png}%
    \end{adjustbox}
\end{figure}
\end{minipage}

\clearpage

\begin{lstlisting}
; *******************************************************************
; gofeed
; A 'demo_routine' routine
; Implements the game over display effect.
; *******************************************************************
gofeed:
        move.l #(PITCH1|PIXEL16|WID384),d0   ; Set the GPU flags in d0.
        move.l d0,source_flags     ; Pass them to the GPU's source flags.
        move.l d0,dest_flags
        lea in_buf,a0              ; Point our GPU RAM input buffer at a0.
        add.l #$2000,pongz
        move pongz,d0
        and.l #$0f,d0              ; Keep it between 0 and 15
        sub.l #$07,d0
        move.l cscreen,(a0)        ; source screen is already-displayed screen
        move.l #384,4(a0)          ; Full screen width.
        move.l #240,d1             ; Full screen height.
        add palfix1,d1             ; Adjust for PAL
        move.l d1,8(a0)            ; Store height in GPU input buffer.
        move.l #$1f4,12(a0)        ; Store X scale in GPU input buffer.
        move.l #$1f4,16(a0)        ; Store Y scale in GPU input buffer.
        move.l d0,20(a0)           ; Store initial angle in brads in GPU buffer.
        move.l #$c00000,24(a0)     ; source x centre in 16:16
        move.l #$780000,d0
        add.l palfix3,d0           ; Adjust for PAL screens if required.
        move.l d0,28(a0)           ; y centre the same
        move.l #$0,32(a0)          ; offset of dest rectangle
        move.l delta_i,36(a0)      ; change of i per increment
        ; Op 2 of this module is Scale and Rotate ('scar').
        move.l #2,gpu_mode         ; Select 'scar' in antelope.gas.
        move.l #demons,a0          ; Load the GPU module in antelope.gas.
        jsr gpurun                 ; Run the selected GPU module.
        jsr gpuwait                ; Wait for the GPU to finish.
    
        ; Calculate the new X and Y scale for the 'Game Over' graphic.
        move.l #4,gpu_mode         ; Select 'rex' in camel.gas. Multiple images stretching towards you in Z
        lea p_sines,a0             ; Load the positive sine table to a0.
        move pongx,d0
        move pongy,d3
        add #15,pongx
        add #17,pongy
        and #$ff,d0                ; Keep it between 0 and 255
        and #$ff,d3                ; Keep d3 between 0 and 255.
        move.b 0(a0,d0.w),d1
        move.b 0(a0,d3.w),d2
        and.l #$ff,d1              ; Keep it between 0 and 255
        and.l #$ff,d2              ; Keep it between 0 and 255
        lsl.l #6,d2                ; Multiply it by 64.
        lsl.l #6,d1                ; Multiply it by 64.
        add.l #$8000,d1
        add.l #$8000,d2
    
        ; Draw the Game Over graphic.
        lea in_buf,a0              ; Point our GPU RAM input buffer at a0.
        move.l #pic2,(a0)+         ; crex->_bass: srce screen for Game Over graphics is pic2
        move.l #$150094,(a0)+      ; crex-> spixel: srce start pixel address for 'Game Over'
        move.l #$440091,(a0)+      ; crex-> ssize: srce size for 'Game Over'
        move.l d1,(a0)+            ; crex-> scalex: x-scale calculated above
        move.l d2,(a0)+            ; crex-> scaley: y-scale calculated above
        move.l #0,(a0)+            ; crex-> shearx: shearx
        move.l #0,(a0)+            ; crex-> sheary: sheary
        move.l #1,(a0)+            ; crex-> mode: Mode 1 = Centered
        move.l #0,d0               ; Store 0 in d0 for X and Y destination position..
        move.l d0,(a0)+            ; crex-> dstx: Destination X position.
        move.l d0,(a0)+            ; crex-> dsty: Destination Y position.
        move.l #$600000,d0         ; Store Z in d0.
        move.l d0,(a0)+            ; crex-> dstz:
        lea parrot,a0              ; Load the GPU module in camel.gas.
        jsr gpurun                 ; do clear screen
        jsr gpuwait                ; Wait for the GPU to finish.
    
        tst timer
        bmi babb
        sub #1,timer
        bpl rrrts
    
babb:   move.l pad_now,d0
        and.l #allbutts,d0
        beq rrrts
        move #50,timer
        move.l #clearfeed,demo_routine
        move.l #$1f4,pongx
        rts
\end{lstlisting}

