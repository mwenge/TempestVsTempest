\chapter{meltovision}
\label{sec:meltovision}
\lstset{style=68KStyle}
\lhead[tempest 2000]{}

\begin{lstlisting}[escapechar=\%]
fade:
;
; go into FADE after merging screen3 to current screen and turning off BEASTIES+64

        tst beasties+76
        bmi ofade
        move.l #screen3,a0
        move.l gpu_screen,a1
        moveq #0,d0
        moveq #0,d1
        move #384,d2
        move #240,d3
        add palfix1,d3
        moveq #0,d4
        moveq #0,d5
        tst mfudj
        beq pmf2
        sub #8,d3
pmfade: tst mfudj
        beq pmf2 
        add #8,d5
        clr mfudj
pmf2:   jsr MergeBlock
        move #-1,beasties+76


ofade:
;       move pauen,-(a7)
        clr _pauen                      ;can't pause in fade
        move.l #ffade,demo_routine
        move.l #failcount,routine
        move #150,pongx
        jsr rannum
        and #$7,d0
        sub #$03,d0
        and #$ff,d0
        move d0,pongz
;       clr pongz
        move.l #$f80000,delta_i
        move z,-(a7)
        clr z
        bsr gogame
        move.l #screen3,a0
        jsr clrscreen
        move (a7)+,z
;       move #-1,db_on
;       move #1,screen_ready
        move #1,sync
;       move (a7)+,pauen
        rts
\end{lstlisting}[escapechar=\%]

\begin{lstlisting}[escapechar=\%]
ffade:  add #1,pongy
        move pongy,d0
        and #$03,d0
        bne failfade
        tst.b pongz+1
        beq failfade
        bmi ffinc
        sub.b #2,pongz+1
ffinc:  add.b #1,pongz+1
failfade:
        move.l #(PITCH1|PIXEL16|WID384),d0
        move.l d0,source_flags
        move.l d0,dest_flags
        lea in_buf,a0
        move pongz,d0
        and.l #$ff,d0
        move.l cscreen,(a0)             ;source screen is already-displayed screen
        move.l #384,4(a0)
        move.l #240,d1
        add palfix1,d1
        move.l d1,8(a0)         ;X and Y dest rectangle size
        move.l #$1f4,12(a0)
        move.l #$1f4,16(a0)             ;X and Y scale as 8:8 fractions
        move.l d0,20(a0)                        ;initial angle in brads
        move.l #$c00000,24(a0)          ;source x centre in 16:16
        move.l #$780000,d0
        add.l palfix3,d0
        move.l d0,28(a0)                ;y centre the same
        move.l #$0,32(a0)               ;offset of dest rectangle
        move.l delta_i,36(a0)           ;change of i per increment
        move.l #2,gpu_mode              ;op 2 of this module is Scale and Rotate
        move.l #demons,a0
        jsr gpurun                      ;do it
        jmp gpuwait
\end{lstlisting}[escapechar=\%]
